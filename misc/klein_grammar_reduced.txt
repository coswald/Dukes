            <PROGRAM> ::= <DEFINITIONS>

        <DEFINITIONS> ::= ε
                        | <DEF> <DEFINITIONS>

                <DEF> ::= function <IDENTIFIER> ( <FORMALS> ) : <TYPE>
                             <BODY>

            <FORMALS> ::= ε
                        | <NONEMPTYFORMALS>

    <NONEMPTYFORMALS> ::= <FORMAL> <NONEMPTYFORMALS'>

   <NONEMPTYFORMALS'> ::= ε
                        | , <NONEMPTYFORMALS>

             <FORMAL> ::= <IDENTIFIER> : <TYPE>

               <BODY> ::= <PRINT-STATEMENT> <BODY>
                        | <EXPR>

               <TYPE> ::= integer
                        | boolean

               <EXPR> ::= <SIMPLE-EXPR> <EXPR-END>
	   
	   <EXPR-END> ::= ε
	                | <EXPR-SYMBOL> <EXPR>

        <SIMPLE-EXPR> ::= <TERM> <SIPMLE-EXPR-END>
	
    <SIMPLE-EXPR-END> ::= ε
                        | <SIMPLE-EXPR-SYMBOL> <SIMPLE-EXPR> 

               <TERM> ::= <FACTOR> <TERM-END>
          
	   <TERM-END> ::= ε
	                | <TERM-SYMBOL> <TERM>

             <FACTOR> ::= if <EXPR> then <EXPR> else <EXPR>
                        | <FACTOR-SYMBOL> <FACTOR>
                        | <IDENTIFIER> ( <ACTUALS> )
                        | <LITERAL>
                        | ( <EXPR> )
			
	  <FACTOR-SYMBOL> ::= not
                        | -

        <EXPR-SYMBOL> ::= < 
                        | =

 <SIMPLE-EXPR-SYMBOL> ::= or
                        | +
                        | -

        <TERM-SYMBOL> ::= and
                        | *
                        | /

            <ACTUALS> ::= ε
                        | <NONEMPTYACTUALS>

    <NONEMPTYACTUALS> ::= <EXPR> <NONEMPTYACTUALS'>

   <NONEMPTYACTUALS'> ::= ε
                        | , <NONEMPTYACTUALS>

            <LITERAL> ::= <NUMBER>
                        | <BOOLEAN>

         <IDENTIFIER> ::= <STRING>

    <PRINT-STATEMENT> ::= print ( <EXPR> )
